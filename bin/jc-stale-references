#!/usr/bin/env perl
#
no lib '.';
use Mojo::Base -base;

use open ':std', ':utf8';

# mojo
use Mojo::Pg;

# stdperl
use FindBin;

# cpan
use Config::Tiny;
use Text::Table::Tiny 'generate_table';

# JobCenter
use lib "$FindBin::Bin/../lib";
use JobCenter::JCC::VersionChecker;

# return help string
sub help {
	return <<END
Usage: $FindBin::Script [Options] [Actions/ActionIDs]

Look for stale references in workflows that need updating.

    Actions       Limit results to one or more action names
    ActionIDs     Limit results to one or more action IDs

Options:

    -h,   --help         show this help messgae
    -N,   --no-names     do not show names
    -w,   --workers      show workers announced for stale actions
    -vv*, --verbose      set verbosity level
END
}

exit main(@ARGV);

sub main {

	my %opts = ( verbose => 0 );
	my @action_ids;
	my @actions;

	while (@_) {
		local $_ = shift;
		/^(?:-h|--help)$/     and do { print help(); exit 0 };
		/^(?:-N|--no-names)$/ and do { $opts{no_names}++; next };
		/^(?:-w|--workers)$/  and do { $opts{workers}++; next };
		/^(?:-c|--cutoff)$/   and do { $opts{cutoff} = shift; next };
		/^-(v+)$/             and do { $opts{verbose} += length $1; next };
		/^-verbose$/          and do { $opts{verbose}++; next };
		/^-/                  and do { die "unknown option: $_\n" };
		/^\d+$/               and do { push @action_ids, $_; next };
		push @actions, $_;
	}

	my $cfg = Config::Tiny->read("$FindBin::Bin/../etc/jobcenter.conf");
	die 'config errror: ' . Config::Tiny->errstr() unless $cfg;
	die 'cutoff expects number' if defined $opts{cutoff} and $opts{cutoff} !~ /^\d+$/;

	my $pg = Mojo::Pg->new(
		'postgresql://'
		. $cfg->{admin}->{user}
		. ':' . $cfg->{admin}->{pass}
		. '@' . ( $cfg->{pg}->{host} // '' )
		. ( ($cfg->{pg}->{port}) ? ':' . $cfg->{pg}->{port} : '' )
		. '/' . $cfg->{pg}->{db}
	);

	my $jvc = JobCenter::JCC::VersionChecker->new(db => $pg->db, %opts);

	# find out of date references matching actions or all
	my $rows = $opts{workers} 
		? $jvc->workers(
			cutoff       => $opts{cutoff}
		)
		: $jvc->out_of_date(
			cutoff       => $opts{cutoff},
			action_ids   => \@action_ids,  # match against actions / workflows
			actions      => \@actions,
			workflow_ids => \@action_ids,  # match against owning workflow as well
			workflows    => \@actions,
		);

	if (@$rows > 1) {

		say "stale references found:";

		say generate_table(rows => $rows, header_row => 1);

	} else {

		say "no stale references found:";
	}

	return 0;
}
